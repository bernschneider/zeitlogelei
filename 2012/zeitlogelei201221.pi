/* Zeit Logelei 2012/21 Die fünf Hexen Klarina, Konstanzia, Kremodora, Krudelia und Kunigundula benötigen jeweils noch zwei Zutaten 
für ihre neuen Zaubertränke https://www.zeit.de/2012/21/Spiele-Logelei-21 */
import cp.
main ?=>    %    1           2          3            4              5
    NHexe = ["Klarina", "Krudelia","Kremodora", "Konstanzia", "Kunigundula"],
    %           1        2         3          4           5           6            7           8             9            10
    NZutat = ["Klee","Mangold","Marzipan","Rosmarin","Hausstaub","Morgentau","Juckpulver","Saharasand","Tigertränen","Spinnenbeine"],
    %                 1         2         3          4        5
    NKrankheit = ["Pocken", "Pickel", "Runzeln", "Buckel", "Warzen"],
    Krankheit = new_list(5), Krankheit :: 1..5, all_different(Krankheit), % Krankheit[I] = K <-> Der Trank von Hexe I hilft gegen K
    Hilft = new_list(10), Hilft :: 1..5, % Hilft[Z] = K <-> Zutat Z hilft gegen Krankheit K
    foreach(I in 1..5) count(I, Hilft, 2) end, % Sie benötigen jeweils noch zwei Zutaten für ihre neuen Zaubertränke
    % In jeden Trank muss genau eine Zutat, die gleich viel Buchstaben hat wie der Name der Hexe, die ihn braut:
    LenZ = [len(NZutat[Z]) : Z in 1..10], LenH = [len(NHexe[I]) : I in 1..5],
    foreach(I in 1..5, Z1 in 1..9, Z2 in Z1+1..10) 
        (Hilft[Z1] #= Krankheit[I] #/\ Hilft[Z2] #= Krankheit[I]) #=> (LenZ[Z1] #!= LenZ[Z2] #/\ (LenZ[Z1] #= LenH[I] #\/ LenZ[Z2] #= LenH[I]))
     end, 
    Hilft[5] #= 1  #=> Hilft[1] #= 3, % Wenn Hausstaub gegen Pocken hilft, hilft Klee gegen Runzeln, ..
    Hilft[5] #!= 1 #=> Hilft[6] #= 4, % .. und sonst hilft Morgentau gegen Buckel    
    Hilft[6] #= 2  #=> Hilft[1] #= Krankheit[2], % Wenn Morgentau gegen Pickel hilft, benötigt Krudelia Klee
    Hilft[3] #= 5, % In den Trank gegen Warzen soll Marzipan
    Hilft[4] #!= 2, Hilft[4] #!= 3, % Rosmarin hilft weder gegen Pickel noch Runzeln
    % Gegen Buckel braucht man zwei Zutaten, die mit dem gleichen Buchstaben beginnen:
    AnfBst = [ord(S[1])-64 : S in NZutat], % Anfangsbuchtabe
    foreach(I in 1..9, J in I+1..10) (Hilft[I] #= 4 #/\ Hilft[J] #= 4) #=> AnfBst[I] #= AnfBst[J] end,
    Krankheit[1] #= 3, % Klarina braut einen Trank gegen Runzeln
    Krankheit[3] #!= Hilft[5], % Kremodora benötigt keinen Hausstaub
    solve(Krankheit ++ Hilft),
    foreach(I in 1..5)
        printf("%ss Trank hift gegen %s. Er benötigt ", NHexe[I], NKrankheit[Krankheit[I]]), 
        foreach(Z in 1..10, Hilft[Z] = Krankheit[I]) printf("%s ", NZutat[Z]) end, nl
    end, fail.
/* 
Klarinas Trank hift gegen Runzeln. Er benötigt Klee Mangold 
Krudelias Trank hift gegen Pocken. Er benötigt Rosmarin Hausstaub 
Kremodoras Trank hift gegen Warzen. Er benötigt Marzipan Morgentau 
Konstanzias Trank hift gegen Buckel. Er benötigt Saharasand Spinnenbeine 
Kunigundulas Trank hift gegen Pickel. Er benötigt Juckpulver Tigertränen 
no more solutions, korrekt */