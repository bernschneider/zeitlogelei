/* Zeit Logelei 2012/42 Martin hat einen Pfeilewald zusammengestellt
https://www.zeit.de/2012/42/Spiele-Logelei-42 */
import sat, util.
main ?=>
    R = 1, L = 3, % -> = 1, <- = 3
    T = 2, H = 0, % v = 2,  ^  = 0
    Pf = {{R,T,T,L,T,R,L},
          {R,L,L,L,R,R,L},
          {H,T,R,H,R,L,H},
          {T,R,L,L,R,L,H},
          {T,T,T,L,T,T,L},
          {H,R,T,T,T,T,H},
          {R,R,H,H,H,R,L}},
    Board = {{_,_,_,_,3,_,5},
    	     {_,_,_,_,_,_,_},
    	     {_,_,_,1,_,_,_},
    	     {_,3,_,_,_,_,_},
    	     {_,_,_,_,_,_,_},
    	     {_,_,_,_,_,_,_},
    	     {_,_,_,_,_,_,6}}, N = len(Board),
    Vars = array_matrix_to_list(Board), Vars :: 1..N-1,
    foreach(Z in 1..N, S in 1..N)
        if     Pf[Z,S] = R then Koord = [(Z,S1) : S1 in S+1..N]
        elseif Pf[Z,S] = L then Koord = [(Z,S1) : S1 in 1..S-1]
        elseif Pf[Z,S] = T then Koord = [(Z1,S) : Z1 in Z+1..N]
        elseif Pf[Z,S] = H then Koord = [(Z1,S) : Z1 in 1..Z-1]
        end,
        % An Position Z|S steht, auf wie viele verschiedene Ziffern der Pfeil zeigt:
	    nvalue(Board[Z,S], [Board[Z1,S1] : (Z1,S1) in Koord]) 
	end,	
	solve(Vars), CPf = "^>v<",
    foreach(Z in 1..N, S in 1..N)
        printf("%c%d|", CPf[1+Pf[Z,S]],Board[Z,S]),
        if S = N then nl end
    end, fail.
/* 
>5|v4|v4|<2|v3|>1|<5|
>2|<1|<2|<2|>2|>1|<2|
^2|v3|>3|^1|>2|<3|^2|
v3|>3|<1|<2|>2|<3|^2|
v2|v2|v2|<1|v2|v1|<2|
^3|>2|v1|v1|v1|v1|^2|
>6|>5|^4|^2|^3|>1|<6|
CPU time 0.19 seconds, no more solutions, korrekt */