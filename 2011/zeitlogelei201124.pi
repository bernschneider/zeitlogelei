/* Zeit Logelei 2011/24 Tom, ein Biologiestudent, sitzt verzweifelt in der Klausur, denn er hat vergessen, das Kapitel über die Wolfswürmer zu lernen
https://www.zeit.de/2011/24/Spiele-Logelei-24 */
import cp.
main ?=>
    A=1, B=2, C=3, D=4, E=5, F=6, G=7, H=8, I=9, J=10, K=11, L=12,  N=12,
    OK = new_list(N), OK :: 0..1, % 1 = die Aussage ist richtig
    % Das Einzige, was er weiß, ist, dass es bei den Multiple-Choice-Aufgaben immer genau 3 korrekte Antworten gibt.
    sum(OK) #= 3,
    % Und dies sind die angebotenen Antworten:
    % a) Wolfswürmer werden oft von Igelwürmern gefressen.
    % b) Wolfswürmer meiden die Gesellschaft von Eselswürmern.
    % c) Wolfswürmer ernähren sich von Lammwürmern.
    % d) Wolfswürmer leben in der banesischen Tundra.
    % e) Wolfswürmer gehören zur Gattung der Hundswürmer.
    % f) Wolfswürmer sind grau gestreift
    % g) Genau eine der beiden Aussagen b) und e) ist richtig:
    OK[G] #= (OK[B] + OK[E] #= 1), 
    % h) Genau eine der beiden Aussagen a) und d) ist richtig:
    OK[H] #= (OK[A] + OK[D] #= 1), 
    % i) Genau eine der beiden Aussagen c) und h) ist richtig:
    OK[I] #= (OK[C] + OK[H] #= 1), 
    % j) Genau eine der beiden Aussagen f) und i) ist richtig:
    OK[J] #= (OK[F] + OK[I] #= 1), 
    % k) Genau eine der beiden Aussagen c) und d) ist richtig:
    OK[K] #= (OK[C] + OK[D] #= 1),  
    % l) Genau eine der beiden Aussagen d) und h) ist richtig:
    OK[L] #= (OK[D] + OK[H] #= 1), 
    solve(OK),
    print("Die Aussagen "),
    foreach(X in 1..N, OK[X]=1) printf("%c) ", chr(96+X)) end,
    println("sind wahr."),
    fail.
/* Die Aussagen c) d) h) sind wahr.
CPU time 0.001 seconds, no more solutions, korrekt */