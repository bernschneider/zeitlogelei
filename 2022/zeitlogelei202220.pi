/* ZEIT Logelei 2022/20
https://www.zeit.de/zeit-magazin/2022/20/logelei */
import util, sat.

neib(2, X, Y) => 2 * X #= Y #\/ 2 * Y #= X.                         % 2 = lila Kreis
neib(1, X, Y) => 1 #= abs(X - Y).                                   % 1 = gelber Kreis
neib(0, X, Y) => 1 #!= abs(X - Y) #/\ 2 * X #!= Y #/\ 2 * Y #!= X.  % 0 = kein Kreis
	
main ?=>
   N = 7, M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 1..N,
   foreach(L in M.rows() ++ M.columns()) all_different(L) end,
   G=1, L=2, % 0=kein Kreis, 1=Gelber Kreis, 2=Lila Kreis
   HN = {{0,L,0,G,0,L}, % Horizontal Neighbours
   		 {L,G,0,L,G,0},
   		 {0,0,0,G,0,L},
   		 {L,L,0,L,0,0},
   		 {0,0,0,G,0,L},
   		 {L,0,L,0,0,0},
   		 {G,0,0,0,0,L}},
   foreach(Z in 1..N, S in 1..N-1) neib(HN[Z,S], M[Z,S], M[Z,S+1]) end,
   VN = {{0,0,0,G,L,0,0}, % Vertical Neighbours (row-wise)
   		 {0,L,0,0,0,G,G},
   		 {0,G,0,G,L,0,0},
   		 {0,0,G,0,0,0,G},
   		 {L,0,G,G,0,0,0},
   		 {0,G,L,0,0,0,G}},
   foreach(Z in 1..N-1, S in 1..N) neib(VN[Z,S], M[Z,S], M[Z+1,S]) end,
   solve(MVars),
   foreach(Z in 1..N) 
      foreach(S in 1..N) printf("%i|", M[Z,S]) end, nl
   end, fail. /* 
7|1|2|5|4|6|3|
3|6|7|4|2|1|5|
5|3|1|7|6|2|4|
1|2|4|6|3|5|7|
4|7|5|2|1|3|6|
2|4|6|3|5|7|1|
6|5|3|1|7|4|2|
CPU time 0.112 seconds, no more solutions, korrekt! */