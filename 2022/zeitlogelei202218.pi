/* Zeit Logelei 2022/18 Professor Knusi bei den Pula
https://www.zeit.de/zeit-magazin/2022/18/logelei
Professor Knusi hat drei Pula getroffen, die sich über eine einstellige Zahl unterhalten. 
Bei den Pula ist es üblich, abwechselnd wahre und falsche Aussagen zu machen. 
Eine hat sich bei einer Aussage vertan. */
import cp.
main ?=> 
    V :: 1..3, X :: 0..9, % V hat sich vertan, und die einstellige Zahl lautet X
    Panna=1, Pocci=2, Pabi=3, Pula = ["Panna", "Pocci", "Pabi"],
    W = new_array(3,3), W :: 0..1, % W[P,I] = 1 <=> P´s I-te Aussage ist wahr
    foreach(P in 1..3) P #= V #<=> (W[P,1] #= W[P,2] #\/ W[P,2] #= W[P,3]) end,
    W[Panna,1] #= (X #> 6), % Panna: "Die Zahl ist größer als 6."           T
    W[Pocci,1] #= (X #< 5), % Pocci: "Die Zahl ist kleiner als 5."          F
    W[Pabi,1]  #= (X mod 2 #= 0), % Pabi: "Die Zahl ist eine gerade Zahl."  F 
    W[Pocci,2] #= (X #= 2 #\/ X #= 3 #\/X #= 5 #\/X #= 7), % Pocci: "Die Zahl ist eine Primzahl.": T
    W[Panna,2] #= (X #!= 7), % Panna: "Die Zahl ist nicht die 7."           F
    W[Pocci,3] #= (X #= 1), % Pocci: "Die Zahl ist die 1."                  F
    W[Pabi,2]  #= (X #< 5), % Pabi: "Die Zahl ist kleiner als 5."           F
    W[Panna,3] #= (X mod 2 #= 1), % Panna: "Die Zahl ist ungerade."         T 
    W[Pabi,3]  #= (X #!= 6), % Pabi: "Die Zahl ist nicht die 6."            T
    solve(vars(W) ++ [X, V]),
    printf("%s hat sich vertan und die Zahl lautet %d. W = %w", Pula[V], X, W), fail. /* 
Pabi hat sich vertan und die Zahl lautet 7. W = {{1,0,1},{0,1,0},{0,0,1}}
CPU time 0.0 seconds., no more solutions, korrekt */