/* Zeit Logelei 2022/28 Münzkonstellation (1, 2, 5, 10, 20 und 50 Cent) */
import sat, util.
main ?=>
    Vals = [0, 1, 2, 5, 10, 20, 50], % es können Kreise leer bleiben
    Hsum = [48,116,87,101,46], Vsum = [109,17,150,40,82], N = len(Hsum),
    M = new_array(N,N), M :: Vals,
    foreach({Sum,L} in zip(Hsum,M.rows()) ++ zip(Vsum,M.columns())) Sum #= sum(L) end,
    % Es liegen nie gleiche Münzen nebeneinander, noch nicht einmal diagonal:
    foreach(Z in 1..N, S in 1..N, Z1 in Z..Z+1, S1 in S-1..S+1, (Z1,S1)!=(Z,S), Z1=<N, 0<S1, S1=<N) 
        M[Z,S] #!= M[Z1,S1] 
    end,
    solve(M),
    foreach(Z in 1..N, S in 1..N) 
        if M[Z,S] > 0 then printf("%2d|", M[Z,S]) else print("  |") end,
        if S=N then nl end
    end, fail. /* 
 2| 1|20| 5|20|
50| 5|50|10| 1|
 2|10|20| 5|50|
50|  |50|  | 1|
 5| 1|10|20|10|
 CPU time 0.09 seconds, no more solutions, korrekt */