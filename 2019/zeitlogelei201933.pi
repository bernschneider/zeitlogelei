/* Alphametik: Schriftliche Multiplikation 
Beispiel: Zeit Logelei 2019/33
MICH * KANN = FACHMANN, M * KANN = FHMNN usw.
MICH * KANN = HOCHOFEN, M * KANN = HKENC usw. */
import cp, util.

main =>
    mult(["MICH", "KANN", "FHMNN", "HEENN", "OFONN", "KANN", "FACHMANN"]),
    mult(["MICH", "KANN", "HKENC", "MAKNF", "MKINM", "KANN", "HOCHOFEN"]).
/*
5291 * 8300
-----------
41500
 16600
  74700
    8300
-----------
43915300 korrekt

3671 * 5099
-----------
15297
 30594
  35693
    5099
-----------
18718429 korrekt */

mult(L) =>
   Letter = keys(new_set(flatten(L))), % verwendete Buchstaben
   Digit = new_list(len(Letter)), Digit :: 0..9, % eine Ziffer je Buchstabe
   all_different(Digit),
   Zeilen = len(L[1]), % Anzahl der Teilrechnungen, im Beispiel = 4
   LenZeile = max([len(L[2+I]) : I in 1..Zeilen]), % maximale Länge der Zeilen, im Beispiel = 5
   Num = new_array(len(L)), % numerische Repräsentation der Elemente von L
   foreach(I in 1..len(L))
       S = [Digit[find_first_of(Letter, C)] : C in L[I]], % Convert Letter C -> Digit
       to_num(S, Num[I]), S[1] #!= 0
   end,
   Num[1] * Num[2] #= Num[len(L)], % Hauptgleichung (im 1. Beispiel MICH * KANN = FACHMANN)
   L1 = L[1],
   foreach(I in 1..Zeilen) % zeilenweise Teilrechnung:
       Digit[find_first_of(Letter, L1[I])] * Num[2] #= Num[2+I]
   end,
   solve(Digit ++ Num),
   printf("%d * %d%n", Num[1], Num[2]),
   printf("-----------%n"),
   foreach(I in 1..Zeilen)
       foreach(_ in 2..I+LenZeile-len(L[2+I])) print(' ') end, % Indent
       println(Num[2+I])
   end,
   printf("-----------%n"),
   printf("%d%n%n", Num[len(L)]).

to_num(List, Num) => 
        Len = length(List),
        Num #= sum([List[I]*10**(Len-I) : I in 1..Len]).