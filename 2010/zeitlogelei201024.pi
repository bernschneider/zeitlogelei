/* Zeit Logelei 2010/24 Dreiecksdiagramm
https://www.zeit.de/2010/24/Spiele-Logelei-24 */
import sat, util.
main ?=>
    C12 = [[13], [24,_], [11,_,_], [7,_,15,20], [_,14,_,19,_], [15,_,_,19,_,_], 
           [_,_,10,_,10,_,_], [_,21,11,16,_,_,_,_]],
    C21 = [[_], [18,_], [6,11,_], [_,13,_,_], [_,13,_,_,12], [_,_,_,_,_,18], 
           [_,_,10,_,_,_,_]],
    M = [new_list(I) : I in 1..9], M :: 1..9,
    foreach(L in M ++ M.cols() ++ M.diag()) all_different(L) end,
    foreach(Z in 1..8, S in 1..Z) C12[Z,S] #= M[Z,S] + M[Z+1,S] + M[Z+1,S+1] end,
    foreach(Z in 1..7, S in 1..Z) C21[Z,S] #= M[Z+1,S] + M[Z+1,S+1] + M[Z+2,S+1] end,
    solve(M), 
    foreach(Z in 1..9)
        foreach(_ in 1..10-Z) print(" ") end,
        foreach(S in 1..Z) printf("%d|", M[Z,S]) end, nl
    end,
    nl, fail.
/* 
         3|
        9|1|
       7|8|9|
      1|3|2|5|
     4|2|6|7|8|
    6|7|5|8|4|2|
   5|4|1|9|2|6|7|
  8|9|3|6|7|1|5|4|
 2|5|7|1|9|8|4|3|6|
CPU time 2.331 seconds, no more solutions, korrekt */
cols(M) = [[M[Z,S] : Z in S..9] : S in 1..9]. % M.columns() funktioniert nicht!
diag(M) = [[M[Z-1+S,S] : S in 1..10-Z] : Z in 1..9].