/* Zeit Logelei 2010/36 Bücher sortieren
https://www.zeit.de/2010/36/Spiele-Logelei-36 */
import cp.
main ?=>
    % ID der Bücher = Ranking nach Höhe
    M = {{2,1,3,4,5}, % zweitkleinstes, kleinstes, 3.-kleinstes Buch usw.
         {_,_,_,_,_},
         {_,_,_,_,_},
         {_,_,_,_,_},
         {_,_,_,_,_},
         {_,_,_,_,_},
         {3,4,5,2,1}}, N = len(M), M :: 1..5, % M[T] = Anordnung der Bücher zum Zeitpunkt T
    foreach(T in 2..N-1) all_different(M[T]) end,
    Name = ["Nachtisch","Fisch","Kuchen","Suppen","Salate"], 
    Alpha = [3,1,2,5,4], % kleinstes=alphabetisch an 3. Stelle, zweitkleinstes=alphabetisch erstes 
    Dicke = [1,4,3,5,2], % kleinstes=dünnstes, zweitkleinstes=4.-dünnstes,  
    Tina=1, Erika=2, Klaus=3,
    P = [Erika, Klaus, Tina, Klaus, Erika, Tina], % Reihenfolge, wer Bücher tauscht
    Sw = new_array(N-1,2), Sw :: 1..5, % Switch Sw[T,1] and Sw[T,2]
    foreach(T in 1..6)
        Sw[T,1] #< Sw[T,2], 
        matrix_element(M, T, Sw[T,1], M1), matrix_element(M, T, Sw[T,2], M2),
        if     P[T] = Tina then % dünnere Bücher sollten im Küchenregal weiter links stehen als dicke!
               element(M1,Dicke,D1), element(M2,Dicke,D2), D1 #> D2
        elseif P[T] = Erika then % alphabetisch von links nach rechts sortieren!
               element(M1,Alpha,A1), element(M2,Alpha,A2), A1 #> A2
        elseif P[T] = Klaus then % die hohen Bücher solllten weiter rechts stehen
               M1 #> M2
        end,
        foreach(K in 1..5) % tausche die Positionen Sw[T,1] und Sw[T,2]:
            K #= Sw[T,1] #=> M[T+1,K] #= M2, 
            K #= Sw[T,2] #=> M[T+1,K] #= M1,
            (K #!= Sw[T,1] #/\ K #!= Sw[T,2]) #=> M[T+1,K] #= M[T,K]
        end
    end,
    solve(vars(Sw) ++ vars(M)),
    foreach(T in 1..N, I in 1..5) 
        printf("%9s ", Name[M[T,I]]),
        if T<N, I=5 then printf("Switch %d, %d%n", Sw[T,1], Sw[T,2]) end
    end, nl, nl, fail.
/*    1        2          3        4          5  
    Fisch Nachtisch    Kuchen    Suppen    Salate Switch 2, 3
    Fisch    Kuchen Nachtisch    Suppen    Salate Switch 1, 3
Nachtisch    Kuchen     Fisch    Suppen    Salate Switch 2, 5
Nachtisch    Salate     Fisch    Suppen    Kuchen Switch 2, 4
Nachtisch    Suppen     Fisch    Salate    Kuchen Switch 1, 5
   Kuchen    Suppen     Fisch    Salate Nachtisch Switch 3, 4
   Kuchen    Suppen    Salate     Fisch Nachtisch 
CPU time 0.009 seconds, no more solutions, korrekt */