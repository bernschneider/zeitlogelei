/* Zeit Logelei 2010/09 Professor Knusi in Kebusien
https://www.zeit.de/2010/09/Spiele-Logelei-09 */
import cp, util.
main ?=>
    Wa=1, Fe=2, StrGruppe = ["Wablache", "Ferguche"], % In Kebusien leben Wablachen und Ferguchen
    Gru = new_list(6), Gru :: [Wa,Fe], 
    % Derzeit finden Frühjahrsaufmärsche statt, bei denen immer je sechs Personen hintereinander durch die Straßen laufen.
    A=1,B=2,C=3,D=4,E=5,F=6, Str = ["Asi", "Besi", "Cosi", "Dasi", "Esi", "Fasi"],
    Pos = new_list(6), Pos :: 1..6, all_different(Pos), % Marschposition von A..F
    at_least(4, Gru, Wa), % in der Gruppe sind mehr Wablachen als Ferguchen, also mindestens 4
    foreach((P0, P1,G1) in [  % Professor Knusi eilt bei einer dieser Gruppen nebenher und befragt die sechs Teilnehmer:
        (A, B,Wa), (A, D,Wa), % Asi: "Besi ist Wablache und Dasi auch."
        (E, F,Fe), (E, D,Fe), % Esi: "Fasi ist Ferguche und Dasi auch."
        (D, E,Fe), (D, A,Fe), % Dasi: "Esi und Asi sind beides Ferguchen!"
        (F, E,Wa),            % Fasi: "Esi ist Wablache"
        (B, F,Wa),            % Besi: "Fasi ist ein Wablache"
        (C, B,Fe)             % Cosi: "Besi ist ein Ferguche":
    ])      % Kebusianer machen über Leute, die vor einem laufen (kleinere Position), immer falsche Aussagen 
            % und über die Leute, die hinter einem laufen (größere Position), immer wahre Aussagen.
            Pos[P0] #< Pos[P1] #<=> Gru[P1] #= G1
    end,
    solve(Gru ++ Pos),
    Ranking = [(Pos[J],J,Gru[J]) : J in 1..6].sort(),
    println("Hier die Marschreihe:"),
    foreach((P,I,G) in Ranking) printf("%d. %4s (%s)%n", P, Str[I], StrGruppe[G]) end,
    nl, fail.
/* Hier die Marschreihe:
1.  Asi (Wablache)
2. Dasi (Wablache)
3.  Esi (Ferguche)
4. Fasi (Ferguche)
5. Besi (Wablache)
6. Cosi (Wablache)
CPU time 0.001 seconds, no more solutions */