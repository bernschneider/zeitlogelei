/* Zeit Logelei 2020/52
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import cp, util.
main ?=>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,x,h,x},
	     {i,j,x,k,x,l},
	     {m,x,n,o,x,x},
	     {p,x,x,x,x,q},
	     {r,x,x,x,s,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,g,h,i,k,m,n,p,r,s], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,l,m,o,q], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, PRD = 2, FWD = 1, REV = 0, % PRoDukt, ForWard, REVerse
   M = new_array(N,N),
   MVars = array_matrix_to_list(M),
   MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), Aw #= Es - Pw,
   waagrecht(M,g,ZAHL,FWD,Gw), % s.u.
   waagrecht(M,g,QSUM,FWD,QGw), QGw #= RMw,
   waagrecht(M,h,ZAHL,FWD,Hw), is_prime(Hw),
   waagrecht(M,i,ZAHL,FWD,Iw), square(Iw),
   waagrecht(M,k,ZAHL,FWD,Kw), square(Kw),
   waagrecht(M,m,ZAHL,FWD,Mw), is_prime(Mw),
   waagrecht(M,m,ZAHL,REV,RMw), % s.o.
   waagrecht(M,n,ZAHL,FWD,Nw), multiple(Hw,Nw),
   waagrecht(M,p,ZAHL,FWD,Pw), multiple(As,Pw),
   waagrecht(M,r,ZAHL,FWD,Rw), Rw #= Js + Gw,
   waagrecht(M,s,ZAHL,FWD,Sw), is_prime(Sw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), square(As),
   senkrecht(M,b,ZAHL,FWD,Bs), is_prime(Bs),
   senkrecht(M,c,ZAHL,FWD,Cs), Cs #= Iw * Js,
   senkrecht(M,d,ZAHL,FWD,Ds), square(Ds),
   senkrecht(M,e,ZAHL,FWD,Es), 
   senkrecht(M,e,ZAHL,REV,REs), multiple(Sw,REs), 
   senkrecht(M,f,ZAHL,FWD,Fs), is_prime(Fs), 
   senkrecht(M,j,ZAHL,FWD,Js), % s.o.
   senkrecht(M,j,ZAHL,PRD,QpJs), square(QpJs),
   senkrecht(M,l,ZAHL,FWD,Ls), is_prime(Ls),
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms),
   senkrecht(M,o,ZAHL,FWD,Os), square(Os),
   senkrecht(M,q,ZAHL,FWD,Qs), is_prime(Qs),
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl, fail. 
   /*
1 1 2 4 9 7 
2 1 6 8 7 1 
1 2 1 4 4 1 
7 1 7 1 7 1 
8 6 2 2 4 6 
4 3 3 1 3 1 
CPU time 0.57 seconds für die 1. Lösung, no more solutions (57 sec.), korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0.
   
is_prime(X) =>
   PR = primes(99),
   element(_,PR,X).

square(X) => 
   SQ = [I*I : I in 1..32],
   element(_,SQ,X).

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N)
					if Len == 0, membchk(LM[R,Cn],WLetter) then Len := Cn - Col end
				end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
     		 M[Row,Col+I] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N)
					if Len == 0, membchk(LM[Rn,C],SLetter) then Len := Rn - Row end
				end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
      		M[Row+I,Col] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List),
        if     Fwd=0 then Num #= sum([List[I]*Base**(I-1) : I in 1..Len])   % REV
        elseif Fwd=1 then Num #= sum([List[I]*Base**(Len-I) : I in 1..Len]) % FWD
        else              Num #= prod(List)                                 % PRD
        end.