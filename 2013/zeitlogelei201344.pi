/* Zeit Logelei 2013/44 (nach Janko Pentominos)
https://www.zeit.de/2013/44/spiele-logelei-44 */
import sat, util.
main =>
    Scnt = [3,5,5,2,2,5,7,2,7,3,4], N = len(Scnt), Np = sum(Scnt)//5, % Zahl der Pentominos
    Zcnt = [4,3,7,1,3,5,5,2,3,9,3],
    M = new_array(N,N), M :: 0..Np,
    % Die Pentominos dürfen einander weder orthogonal noch diagonal berühren:
    foreach(Z in 1..N, S in 1..N-1) M[Z,S] #= M[Z,S+1] #\/ M[Z,S] #=< 0 #\/ M[Z,S+1] #=< 0 end,
    foreach(Z in 1..N-1, S in 1..N) M[Z,S] #= M[Z+1,S] #\/ M[Z,S] #=< 0 #\/ M[Z+1,S] #=< 0 end,
    foreach(Z in 1..N-1, S in 1..N-1) 
        M[Z,S] #= M[Z+1,S+1] #\/ M[Z,S] #=< 0 #\/ M[Z+1,S+1] #=< 0,
        M[Z,S+1] #= M[Z+1,S] #\/ M[Z,S+1] #=< 0 #\/ M[Z+1,S] #=< 0 
    end,
    % Die Zahlen links und oben geben an, wie viele Felder des Diagramms in der entsprechenden Zeile bzw. Spalte von Pentominos bedeckt sind:
    foreach({Cnt,L} in zip(Scnt,M.columns()) ++ zip(Zcnt,M.rows()))
        Cnt #= sum([(V #> 0) : V in L])
    end,
    P = [{{1,1,1}, {0,1,0}, {0,1,0}}, % die vier T-Pentominos
         {{0,0,1}, {1,1,1}, {0,0,1}},
         {{0,1,0}, {0,1,0}, {1,1,1}},
         {{1,0,0}, {1,1,1}, {1,0,0}}], 
    foreach(I in 1..Np) count(I, vars(M), 5) end, % alles Pentominos
    Idx = new_list(Np), Idx :: 1..4, % T hat 4 Drehvarianten
    DZ = new_list(Np), DZ :: 0..N-3, NZ = 3, % Translation des I.ten Pentominos
    DS = new_list(Np), DS :: 0..N-3, NS = 3, 
    foreach(I in 1..Np, J in 1..4) % Constraint nur, wenn Idx[I] #= J!
        foreach(DZI in 0..N-3, DSI in 0..N-3, Z in 1..NZ, S in 1..NS, P[J,Z,S]=1) 
            (Idx[I] #= J #/\ DZ[I] #= DZI #/\ DS[I] #= DSI) #=> M[DZI+Z,DSI+S] #= I
        end
    end,
    solve(vars(M) ++ DZ ++ DS ++ Idx),
    foreach(Z in 1..N)
        foreach(S in 1..N)
		    printf("%c|", cond(M[Z,S]>0, 'X', ' '))
    	end, nl
    end. /*
 | | | |X|X|X| |X| | |
 |X| | | |X| | |X| | |
 |X|X|X| |X| |X|X|X| |
 |X| | | | | | | | | |
 | | | | |X|X|X| | | |
 |X|X|X| | |X| | | |X|
 | |X| | | |X| |X|X|X|
 | |X| | | | | | | |X|
X| | | | | |X| |X| | |
X|X|X| |X|X|X| |X|X|X|
X| | | | | |X| |X| | |
CPU time 44.241 seconds korrekt */