/* Zeit Logelei 2013/50 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main ?=>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,x,j,k,l},
	     {m,n,o,x,p,x},
	     {q,x,r,s,x,x},
	     {t,x,u,x,x,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,e,g,h,i,k,m,o,q,s,t,u], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,l,n,p,q,r], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), % s.u.
   waagrecht(M,a,ZAHL,REV,RAw), multiple(Ew,RAw),
   waagrecht(M,e,ZAHL,FWD,Ew), is_prime(Ew), % s.o.
   waagrecht(M,g,ZAHL,FWD,Gw), square(Gw),
   waagrecht(M,h,ZAHL,FWD,Hw), multiple(Hw,Iw),
   waagrecht(M,i,ZAHL,FWD,Iw), multiple(Rs,Iw), % s.o.
   waagrecht(M,k,ZAHL,FWD,Kw), is_prime(Kw),
   waagrecht(M,m,ZAHL,FWD,Mw), is_prime(Mw),  
   waagrecht(M,o,ZAHL,FWD,Ow), Ow #= Es * Fs,
   waagrecht(M,q,ZAHL,FWD,Qw), square(Qw),
   waagrecht(M,s,ZAHL,FWD,Sw), square(Sw),
   waagrecht(M,t,ZAHL,FWD,Tw), square(Tw),
   waagrecht(M,u,LUw), prod(LUw) #= Ls,
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), multiple(RQs,As),
   senkrecht(M,b,ZAHL,FWD,Bs), is_cube(Bs),               
   senkrecht(M,c,ZAHL,FWD,Cs), Cs #= Ds * Ds,
   senkrecht(M,d,ZAHL,FWD,Ds), multiple(Fs,Ds), % s.o. 
   senkrecht(M,d,ZAHL,REV,RDs), % s.u.
   senkrecht(M,e,ZAHL,FWD,Es), square(Es), % s.o.
   senkrecht(M,f,ZAHL,FWD,Fs), square(Fs), % s.o.    
   senkrecht(M,j,ZAHL,FWD,Js), Js #= As + Aw, 
   senkrecht(M,l,ZAHL,FWD,Ls), multiple(RDs,Ls), % s.o.
   senkrecht(M,n,ZAHL,FWD,Ns), multiple(Qs,Ns), 
   senkrecht(M,p,ZAHL,FWD,Ps), square(Ps), 
   senkrecht(M,q,ZAHL,FWD,Qs), is_prime(Qs),
   senkrecht(M,q,ZAHL,REV,RQs), % s.o.
   senkrecht(M,r,ZAHL,FWD,Rs), is_prime(Rs), % s.o. 
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl, fail.
/*
4 1 5 7 1 3 
1 2 1 2 2 6 
8 5 8 8 1 3 
2 9 4 3 5 6 
4 4 1 3 2 4 
3 6 9 9 9 5 
CPU time 10.019 seconds, no more solutions, korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0, A #!= X.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..99], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).