/* Zeit Logelei 2017/41
http://www.zeit.de/2017/41/spiele-logelei-41*/
import cp.

go1 =>
   Vars = [H,U,P,E,I,Z,A],
   Vars :: 0..9,
   all_different(Vars),
   to_num([H,U,P,E], HUPE), to_num([P,I,Z,Z,A], PIZZA), 
   H #!= 0, P #!= 0, 
   2 * HUPE #= PIZZA,
   solve_all(Vars) = Sols, println(Sols),
   solve(Vars),
   printf(" %d\n",HUPE), 
   printf(" %d\n",HUPE), 
   print("------\n"),
   print(PIZZA). 
/*
 7614
 7614
------
15228 korrekt */
   
go2 =>
   Vars = [H,U,P,E,A,T,R,O],
   Vars :: 0..9,
   all_different(Vars),
   to_num([H,U,P,E], HUPE), to_num([A,T,T,R,A,P,P,E], ATTRAPPE),  
   to_num([R,H,P,P,H], RHPPH), to_num([O,T,U,P,U], OTUPU), 
   H #!= 0, O #!= 0, A #!= 0,
   H    * HUPE #= RHPPH, 
    U   * HUPE #=  OTUPU,
     P  * HUPE #=   0,
      E * HUPE #=     HUPE,
   HUPE * HUPE #= ATTRAPPE,
   solve(Vars),
   println("[H,U,P,E,A,T,R,O]"),
   println(Vars),
   printf("%d * %d\n",HUPE, HUPE),
   println("-----------"),
   printf("%d%n", H * HUPE),
   printf(" %d%n", U * HUPE),
   printf("    %d%n",  E * HUPE),
   println("--------"),
   printf("%d%n", ATTRAPPE).
/*
[H,U,P,E,A,T,R,O]
[8,5,0,1,7,2,6,4]
8501 * 8501
-----------
68008
 42505
    8501
--------
72267001 !korrekt */

to_num(List, Num) => 
        Len = length(List),
        Num #= sum([List[I]*10**(Len-I) : I in 1..Len]).

