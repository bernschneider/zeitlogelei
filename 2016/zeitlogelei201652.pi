/* Zeit Logelei 2016/52 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import cp.
import util.
main => go.

go =>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,j,x,k,x},
	     {l,x,x,x,x,m},
	     {n,o,x,p,x,x},
	     {q,x,r,x,s,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,c,e,g,h,i,l,n,p,q,r,s], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,k,l,m,o,p], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N),
   MVars = array_matrix_to_list(M),
   MVars :: 1..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), waagrecht(M,c,QSUM,FWD,QCw), Aw #= QCw,
   waagrecht(M,c,ZAHL,FWD,Cw), is_prime(Cw),
   waagrecht(M,e,ZAHL,FWD,Ew), waagrecht(M,e,ZAHL,REV,REw), Ew #= REw, % Palindrom
   waagrecht(M,g,ZAHL,FWD,Gw), waagrecht(M,a,ZAHL,REV,RAw), multiple(RAw,Gw),
   X #= M[2,4], X #= M[2,5], X #= M[2,6], % In Hw sind alle Ziffern gleich!
   waagrecht(M,i,ZAHL,FWD,Iw), Iw #= Fs * Ks,
   waagrecht(M,l,ZAHL,FWD,Lw), multiple(Ms,Lw),
   waagrecht(M,n,ZAHL,FWD,Nw), multiple(Qw,Nw),
   waagrecht(M,p,ZAHL,FWD,Pw), multiple(Pw,Bs),
   waagrecht(M,q,ZAHL,FWD,Qw), square(Qw),
   waagrecht(M,r,ZAHL,FWD,Rw), square(Rw),
   waagrecht(M,s,ZAHL,FWD,Sw), is_prime(Sw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), square(As),
   senkrecht(M,b,ZAHL,FWD,Bs), square(Bs),
   senkrecht(M,c,ZAHL,FWD,Cs), square(Cs),
   senkrecht(M,d,ZAHL,FWD,Ds), square(Ds),
   senkrecht(M,e,ZAHL,FWD,Es), square(Es),
   senkrecht(M,f,ZAHL,FWD,Fs), square(Fs),
   senkrecht(M,j,ZAHL,FWD,Js), square(Js),
   senkrecht(M,k,ZAHL,FWD,Ks), square(Ks),
   senkrecht(M,l,ZAHL,FWD,Ls), square(Ls),
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms),
   senkrecht(M,o,ZAHL,FWD,Os), square(Os),
   senkrecht(M,p,ZAHL,FWD,Ps), square(Ps),
   solve(MVars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl.
/* 
1 1 4 7 2 2 
4 2 9 5 5 5 
4 9 5 6 1 6 
6 6 1 9 9 1 
7 6 8 4 3 2 
6 4 4 9 6 1 CPU time 2.938 seconds.	
Waagerecht: A 11 C 47 E 22 G 429 H 555 I 495616 L 661991 N 768 P 432 Q 64 R 49 S 61
Senkrecht: A 144 B 1296 C 49 D 7569 E 25 F 256 J 5184 K 1936 L 676 M 121 O 64 P 49 */

multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0.
   
is_prime(X) =>
   Max = fd_max(X),
   foreach(I in 2..Max // 2)
      I #!= X #=> X mod I #> 0
   end.

square(X) =>
   Max = fd_max(X),
   Tmp :: 0..Max,
   X #= Tmp**2.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N)
					if Len == 0, membchk(LM[R,Cn],WLetter) then Len := Cn - Col end
				end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
     		 M[Row,Col+I] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N)
					if Len == 0, membchk(LM[Rn,C],SLetter) then Len := Rn - Row end
				end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
      		M[Row+I,Col] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).