/* Zeit Logelei 2015/20 */
import cp, util.

go =>
   N = 7, Max = 5,   % 7 Zeilen x 7 Spalten, Ziffern von 0..5
   SigZ = [ [6,0,0], [0,1,3], [10,0],  [15], [6,7],   [0,5], [7,8] ], % Zeilensignatur (7x), 0 = ?
   SigS = [ [4],     [7,0,0], [0,3,0], [0],  [0,1,0], [0]          ], % Spaltensignatur (6x)
   Board = new_array(N+1,N+1), Board[N+1,N+1] #= 0,	% End-0er angefügt für die Blockbildung
   foreach(I in 1..N) Board[I,N+1] #= 0, Board[N+1,I] #= 0 end,  
   Boardvars = array_matrix_to_list(Board), Boardvars :: 0..Max,  
   foreach(Row in rows(Board)) all_different_except_0(Row) end,
   foreach(Col in columns(Board)) all_different_except_0(Col) end,
   foreach(Z in 1..N)   signature(N, Max, L, SigZ[Z]), L #= [Board[Z,S] : S in 1..N+1] end,
   foreach(S in 1..N-1) signature(N, Max, L, SigS[S]), L #= [Board[Z,S] : Z in 1..N+1] end,
   solve([ff,down], Boardvars), print_board(N, Board),
   solve_all([ff,down], Boardvars) = Sols, print(numbersols = len(Sols)).
/*	 0 1 5 0 3 0 2
	 0 2 0 1 0 0 3
	 0 4 1 5 0 2 0
	 0 0 2 3 1 4 5
	 1 5 0 0 0 3 4
	 3 0 0 0 4 1 0
	 0 3 4 0 2 5 1	CPU time 2.2 seconds. 
	 #15#3#2, #2#1##3, #415#2#, ##23145, 15###34, 3###41#, #34#251 */

signature(N, Max, L, Sig) =>     % berechnet L aus Sig! Kein solve(Idx) nötig!
	L = new_list(N+1), L :: 0..Max, L[N+1] #= 0,
	Len = len(Sig), Idx = new_list(Len+1), Idx :: 0..N+1,       % Indices der End-0er
	foreach(J in 1..Len)
    	I0 #= Idx[J], I1 #= Idx[J+1], I0 #< I1, I1 #> 1,
    	element(I1,L,0),        								% I1 zeigt auf eine 0
    	sum([ L[I] * (I0 #< I) * (I #< I1) : I in 1..N]) #= S,  % berechne die Teilsumme S
    	S #> 0, if Sig[J] > 0 then S #= Sig[J] end				% S>0 und ggfs. die Zahl in der Signatur!
    end,
    % Vor dem ersten Index stehen nur 0er:
    foreach(I in 1..N) (Idx[1] #< I) #\/ (L[I] #= 0) end,
    % 0er hinter Nicht-0ern müssen ein Index sein (dürfen nicht sonstwo stehen): > 1 5 >0 0 0 3 4 >0
    foreach(I in 2..N+1) (L[I] #= 0  #/\ L[I-1] #> 0) #=> sum([(Idx[J] #= I) : J in 1..Len+1]) #> 0 end.
    
print_board(N, Board) =>
   foreach(I in 1..N, J in 1..N)
      printf(" %1d", Board[I,J]),
      if J = N then nl end
   end.