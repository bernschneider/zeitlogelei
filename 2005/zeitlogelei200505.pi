/* Zeit Logelei Nr. 2005/05 (2005/04 ist ein Kreuzzahlrätsel)
https://www.zeit.de/2005/05/Spielen_2fLogelei_05/komplettansicht
1. Professor Flausel soll eine chemische Substanz daraufhin untersuchen, welche Chemikalien sich in ihr verbergen. Dazu hat er eine Reihe von Untersuchungen durchgeführt.
Die Untersuchungen bestehen alle aus Tests, die dem gleichen Schema entsprechen: Wenn mindestens eines von mehreren Kriterien erfüllt ist, schlagen sie an, 
wenn keines der Kriterien erfüllt ist, schlagen sie nicht an. Hier die Liste der Tests, die Professor Flausel durchgeführt hat: ...
Sehr zum Erstaunen von Professor Flausel schlagen alle zwölf Tests an. Welche Chemikalien enthält demnach die Substanz? */
import util, cp.
main1 ?=>
    Substanz = [U, B, C, D, K, L, M, S], Substanz :: 0..1, % Substanz[I] = 1 <=> Substanz enthält Chemikalie I
    StrSubstanz = ["2,4Upetin", "Brummium", "Choralperchlorat", "Diknothylbromat", "Knuselium-Hydroxid", "Logellauge", "Methusaläther", "Systelsäure"],
    foreach(Test in [
        [(0,S),(1,B),(1,C)], % Meiseltest: Keine Systelsäure, Brummium, Choralperchlorat (das heißt: Der Meiseltest schlägt an, wenn die Substanz keine Systelsäure enthält oder wenn sie Brummium enthält oder wenn sie Choralperchlorat enthält).
        [(0,K),(0,B)], % Knuselium-Test: Kein Knuselium-Hydroxid, kein Brummium.
        [(1,S),(0,L)], % Bratschertest: Systelsäure, keine Logellauge.
        [(0,D),(1,B)], % Somistäbchen-Test: Kein Diknothylbromat, Brummium.
        [(1,K),(0,U)], % 2,4-Test A: Knuselium-Hydroxid, kein 2,4Upetin.
        [(0,L),(0,C)], % Bausitest: Keine Logellauge, kein Choralperchlorat.
        [(1,K),(1,U),(0,M),(0,B)], % 2,4-Test B: Knuselium-Hydroxid, 2,4-Upetin, kein Methusaläther, kein Brummium.
        [(1,L),(1,M),(1,K)], % LMKH-Test: Logellauge, Methusaläther, Knuselium-Hydroxid.
        [(1,B),(0,S),(1,L)], % Pump-Test: Brummium, keine Systelsäure, Logellauge.
        [(0,M),(1,D)], % Sonamonin-Test: Kein Methusaläther, Diknothylbromat.
        [(1,D),(1,S),(0,K)], % Dick-Heuter-Test: Diknothylbromat, Systelsäure, kein Knuselium-Hydroxid.
        [(1,D),(0,B),(0,L),(0,S)] ])% Rasenhaar-Test: Diknothylbromat, kein Brummium, keine Logellauge, keine Systelsäure. */
            sum([(Sub #= W) : (W,Sub) in Test]) #> 0 
    end,
    solve(Substanz),
    foreach(I in 1..len(Substanz), Substanz[I] = 1)
        printf("%s ", StrSubstanz[I])
    end, nl, fail. /* 
Brummium Diknothylbromat Logellauge Systelsäure 
CPU time 0.001 seconds, no more solutions, korrekt */
/* 2. Martin möchte von Gertraud, Nadja und Ursel wissen, wie alt sie sind. Auf seine Frage bekommt er folgende Antworten: ... */
main2 ?=>
    Alter = [G,N,U], Alter :: 1..100,
    G - N #= U, % Gertraud: "Als ich so alt war wie Nadja, kam Ursel gerade auf die Welt.":
    Delta1 #= U - N, % Nadja: "Wenn ich so alt sein werde, wie Ursel jetzt ist, ..
    G + Delta1 #= 3 * N, % .. wird Gertraud dreimal so alt sein, wie ich jetzt bin."
    N + 12 + G + 12 #= 5 * U, % Ursel: "In zwölf Jahren sind Nadja und Gertraud zusammen genau fünfmal so alt, wie ich jetzt bin.":
    solve(Alter),
    printf("Gertraud ist %d, Nadja ist %d und Ursel ist %d Jahre alt%n", G, N, U),
    fail.
/* 2. Gertraud ist 15, Nadja ist 6 und Ursel ist 9 Jahre alt
CPU time 0.0 seconds, no more solutions, korrekt */