/* Zeit Logelei Nr. 2005/13
https://www.zeit.de/2005/13/Spielen_2fLogelei_13/komplettansicht
Professor Knusi ist auf einer Forschungsreise in Teralien. Dort gibt es drei Arten von Bewohnern. Die einen sagen immer die Wahrheit, die nächsten lügen immer. 
Die dritte Art von Bewohnern sagt an dem Tag, an dem sie sprechen lernt, immer die Wahrheit. Am nächsten Tag lügt sie immer, am darauf folgenden Tag 
sagt sie wieder die Wahrheit et cetera, immer abwechselnd. Wie man sich denken kann, möchte Professor Knusi jetzt herausfinden, wer zu welcher Gruppe gehört 
und hat acht Bewohner über ihre Angewohnheiten befragt. Hier die Antworten: .. Wenn man dabei noch bedenkt, dass Bewohner von der dritten Art an verschiedenen 
Tagen das Sprechen gelernt haben und es somit sein kann, dass sie gerade einen Wahrheitstag haben, genauso wie es sein kann, dass sie einen Lügentag haben, 
wer der acht gehört dann zu welcher Art? */
import cp.
main ?=>
    A=1, B=2, C=3, D=4, E=5, F=6, G=7, H=8, 
    W = new_array(2,8), W :: 0..1, % W[t,i] = 1 <-> Am Tag t sagt i die Wahrheit (heute ist t=1, morgen ist t=2)
    % S[I] = 0 heißt "I lügt immer", S[I] = 1 heißt "I wechselt ab", S[I] = 2 heißt "I sagt immer die Wahrheit":
    S = new_list(8), foreach(I in 1..8) S[I] #= W[1,I] + W[2,I] end, 
    W[1,A] #= (W[1,D] #= 0), % Arbel: »Derbel lügt heute.«
    W[1,B] #= (W[1,E] + W[1,F] #= 0), % Borbel: »Erbel und Firbel lügen heute.«
    (W[1,C] #= W[2,C]) #= (S[D] #= 1), % Curbel: »Morgen würde ich sagen, dass Derbel abwechselnd die Wahrheit sagt und lügt.«
    W[1,D] #= (W[1,F] + W[1,A] #= 1), % Derbel: »Genau einer von Firbel und Arbel sagt heute die Wahrheit.«
    % Erbel: »Einer von Curbel, Firbel und Garbel sagt immer die Wahrheit, einer lügt immer, und einer sagt abwechselnd die Wahrheit und lügt.«:
    W[1,E] #= ( S[C] #!= S[F] #/\ S[C] #!= S[G] #/\ S[F] #!= S[G] ), % d.h. all_different
    W[1,F] #= (W[1,G] #= 0 #/\ W[2,A] #= 1), % Firbel: »Garbel lügt heute, und Arbel sagt morgen die Wahrheit.«
    W[1,G] #= (S[D] #= 1 #/\ S[H] #= 1), % Garbel: »Derbel und Hurbel sagen beide abwechselnd die Wahrheit und lügen.«
    W[1,H] #= (sum([(S[I] #= 2) : I in 1..8]) #= 1), % Hurbel: »Genau einer von uns acht sagt immer die Wahrheit.«
    W[1,A] #= (S[F] #= 2), % Arbel: »Firbel sagt immer die Wahrheit.«
    W[1,B] #= (sum(W[2]) #= 5), % Borbel: »Morgen werden genau drei von uns lügen.«
    W[1,C] #= (S[G] #= 1), % Curbel: »Garbel sagt abwechselnd die Wahrheit und lügt.«
    W[1,D] #= (8 - sum(W[1]) #= sum(W[2])), % Derbel: »Heute lügen genauso viele von uns, wie gestern die Wahrheit gesagt haben.«
    W[1,E] #= (sum([(S[I] #= 0) : I in [F, B, D]]) #= 1), % Erbel: »Genau einer von Firbel, Borbel und Derbel lügt immer.«
    W[1,F] #= (W[2,C] #= 0), % Firbel: »Morgen wird Curbel lügen.«
    W[1,G] #= (W[1,A] #= 1 #\/ W[1,E] #= 1), % Garbel: »Entweder Arbel oder Erbel oder beide sagen heute die Wahrheit.«
    W[1,H] #= (W[1,C] #= 1), % Hurbel: »Curbel sagt heute die Wahrheit.«
    solve(vars(W) ++ S), 
    println("         A B C D E F G H"), println(heute=W[1]), println(morgn=W[2]), println(summe=S),
    nl, fail.  /* 
         A B C D E F G H
heute = {0,0,1,1,0,1,0,1}
morgn = {1,1,0,1,0,0,1,0}
summe = [1,1,1,2,0,1,1,1]
CPU time 0.0 seconds, no more solutions, korrekt */