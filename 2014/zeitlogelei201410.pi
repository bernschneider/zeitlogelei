/* Zeit Logelei 2014/10 Kreuzzahl-Rätsel 
TBD!!!
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main ?=>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,x,x,x},
	     {h,x,x,i,x,x},
	     {j,x,k,l,x,m},
	     {n,x,x,x,x,x},
	     {o,x,p,x,q,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,c,e,g,h,i,j,l,n,o,p,q], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,k,l,m], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), Aw mod 2 #= 0,
   waagrecht(M,a,LAw), % s.u.
   waagrecht(M,c,ZAHL,FWD,Cw), square(Cw),
   waagrecht(M,e,ZAHL,FWD,Ew), is_prime(Ew),
   waagrecht(M,g,ZAHL,FWD,Gw), Gw #= As * As,
   waagrecht(M,h,LHw), palindrom(LHw),
   waagrecht(M,i,ZAHL,FWD,Iw), square(Iw),
   waagrecht(M,j,ZAHL,FWD,Jw), is_cube(Jw),
   waagrecht(M,l,LLw), palindrom(LLw),  
   waagrecht(M,n,ZAHL,FWD,Nw), Nw #= Jw * Js,
   waagrecht(M,o,ZAHL,REV,ROw), ROw #= QPw,
   waagrecht(M,p,QSUM,FWD,QPw), % s.o.
   waagrecht(M,p,ZAHL,FWD,Pw), Pw #= prod(LAw),
   waagrecht(M,q,ZAHL,FWD,Qw), is_prime(Qw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As mod 2 #= 0, % s.o.
   senkrecht(M,b,ZAHL,FWD,Bs), Bs #= Gw + Nw + Es,    
   senkrecht(M,c,ZAHL,FWD,Cs), square(Cs), 
   senkrecht(M,d,ZAHL,FWD,Ds), multiple(Qw,Ds), 
   senkrecht(M,e,ZAHL,FWD,Es), Es #= Fs * Fs, % s.o. 
   senkrecht(M,f,ZAHL,FWD,Fs), Fs #= Cs + RJs, 
   senkrecht(M,j,ZAHL,FWD,Js), % s.o.
   senkrecht(M,j,ZAHL,REV,RJs), square(RJs), % s.o.
   senkrecht(M,k,QSUM,FWD,QKs), QKs #= Ew,
   senkrecht(M,l,LLs), palindrom(LLs), 
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms), 
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl, fail.
/*
6 8 1 6 1 3 
4 1 4 7 3 6 
4 8 4 1 6 9 
5 1 2 8 1 8 
2 6 7 2 6 4 
2 1 4 8 1 1 
CPU time 5.664 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0, A #!= X.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..31], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).