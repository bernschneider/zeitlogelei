/* Zeit Logelei 2014/25
Die Freundinnen Tina, Simone und Jessica haben sich jeweils eine vierstellige Zahl ausgedacht.
Simone: "Dabei haben die Zahlen ganz lustige Eigenschaften. Beispielsweise sind alle drei aufsteigend sortiert. Also jeweils die nächste Ziffer ist größer als die vorherige"
Tina: "Wenn du ihm jetzt noch verrätst, dass die Quersumme meiner Zahl, die übrigens eine Quadratzahl ist, größer ist als die von deiner und die wiederum größer ist als die von Jessica, dann weiß Tom bestimmt gleich unsere Zahlen"
Jessica: "Du musst ihm ja nicht sagen, dass das Querprodukt meiner Zahl größer ist als das Querprodukt von Simones Zahl und das wiederum größer ist als das von deiner Zahl"
"Ach, ihr seid doch doof, da kannst du" – damit wendet sie sich an Tina – "ihm doch auch gleich noch sagen, dass die letzte Ziffer deiner Zahl eine 7 ist." */
import cp. 
go => 
	Tina = new_list(4), 	Tina[1] #> 0,		asc(Tina),
	Simone = new_list(4), 	Simone[1] #> 0,		asc(Simone),
	Jessica = new_list(4), 	Jessica[1] #> 0,	asc(Jessica),
	Vars = Tina ++ Simone ++ Jessica,
	Vars :: 0..9, 
	ST #= sum(Tina), SS #= sum(Simone), SJ #= sum(Jessica), ST #> SS, SS #> SJ,
	PT #= prod(Tina), PS #= prod(Simone), PJ #= prod(Jessica), PJ #> PS, PS #> PT,
	square(ST), Tina[4] #= 7,
	solve(Vars),  
	printf("Tina: %w, Simone: %w, Jessica: %w", Tina, Simone, Jessica).
	% Tina: [1,2,6,7], Simone: [1,3,5,6], Jessica: [2,3,4,5]
	% Die Zahlen lauten 1267, 1356 und 2345
	
	asc(L) =>
		foreach(I in 1..3)
			L[I] #< L[I+1]
		end.
		
	square(X) =>
   		Max = fd_max(X),
   		Tmp :: 0..Max,
   		X #= Tmp**2.