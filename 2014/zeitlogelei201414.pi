/* Zeit Logelei 2014/14 Kreuzzahl-Rätsel 
TBD!!!
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main ?=>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,j,k,x,x},
	     {l,m,n,x,o,x},
	     {p,x,x,q,x,r},
	     {s,x,x,x,t,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,e,g,h,i,j,l,n,p,q,s,t], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,k,m,o,p,r], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), Aw #= Rs * Rs,
   waagrecht(M,e,ZAHL,FWD,Ew), is_prime(Ew),
   waagrecht(M,g,LGw), palindrom(LGw),
   waagrecht(M,h,ZAHL,FWD,Hw), square(Hw),
   waagrecht(M,i,ZAHL,FWD,Iw), is_prime(Iw),
   waagrecht(M,j,ZAHL,FWD,Jw), Jw mod 2 #= 0,
   waagrecht(M,l,ZAHL,FWD,Lw), is_prime(Lw),  
   waagrecht(M,n,LNw), palindrom(LNw),
   waagrecht(M,p,LPw), palindrom(LPw),
   waagrecht(M,q,ZAHL,FWD,Qw), is_cube(Qw),
   waagrecht(M,s,ZAHL,FWD,Sw), multiple(Os,Sw),
   waagrecht(M,t,ZAHL,FWD,Tw), is_prime(Tw),     
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), multiple(Ds,As), 
   senkrecht(M,b,ZAHL,FWD,Bs), square(Bs),    
   senkrecht(M,c,ZAHL,FWD,Cs), square(Cs), 
   senkrecht(M,d,ZAHL,FWD,Ds), is_prime(Ds), % s.o.
   senkrecht(M,e,LEs), palindrom(LEs), 
   senkrecht(M,f,ZAHL,FWD,Fs), Fs #= Ew * Ew, 
   senkrecht(M,j,ZAHL,FWD,Js), Js #= Rs * Tw,
   % senkrecht(M,l,ZAHL,FWD,Ls), multiple(REw,Ls), 
   senkrecht(M,k,LKs), palindrom(LKs),
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms), 
   senkrecht(M,o,ZAHL,FWD,Os), square(Os), % s.o.
   senkrecht(M,p,ZAHL,FWD,Ps), Ps #= Lw - Ds,
   senkrecht(M,r,ZAHL,FWD,Rs), is_prime(Rs), % s.o.
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl, fail.
/*
3 4 8 1 4 1 
1 4 1 1 9 6 
1 1 1 3 4 8 
3 7 1 1 1 1 
2 8 2 1 2 5 
6 4 1 3 1 9 
CPU time 4.494 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0, A #!= X.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..31], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).