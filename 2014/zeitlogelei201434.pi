/* Zeit Logelei 2014/34 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main =>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,j,x,x,x},
	     {k,x,l,m,n,o},
	     {p,x,x,q,x,x},
	     {r,x,x,x,s,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,e,g,h,i,j,k,l,p,q,r,s], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,m,n,o], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), multiple(Ew,Aw), % Aw ist Vielfaches von Ew
   waagrecht(M,e,ZAHL,FWD,Ew), is_prime(Ew),
   waagrecht(M,e,ZAHL,REV,REw),
   waagrecht(M,g,LGw), senkrecht(M,m,LMs), gleicheZiffern(LGw, LMs),
   waagrecht(M,h,ZAHL,FWD,Hw), is_cube(Hw),
   waagrecht(M,i,ZAHL,FWD,Iw), Iw #= QAs,
   waagrecht(M,j,ZAHL,FWD,Jw), % s.u.
   waagrecht(M,j,LJw), gleicheParitaet(LJw),
   waagrecht(M,k,ZAHL,FWD,Kw), Kw #= QBs, 
   waagrecht(M,l,ZAHL,FWD,Lw),
   waagrecht(M,l,LLw), nichtgleicheParitaet(LLw),
   waagrecht(M,p,ZAHL,FWD,Pw), Pw mod 2 #= 0, % gerade
   waagrecht(M,q,Qw), palindrom(Qw),    
   waagrecht(M,r,ZAHL,FWD,Rw), Rw #= Jw + Lw,
   waagrecht(M,s,ZAHL,REV,RSw), multiple(REw,RSw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As #= Ds * Os,
   senkrecht(M,a,QSUM,FWD,QAs), % s.o.
   senkrecht(M,b,ZAHL,FWD,Bs), Bs #= Jw * Fs,
   senkrecht(M,b,QSUM,FWD,QBs), % s.o.
   senkrecht(M,c,ZAHL,FWD,Cs), Cs #= As + Bs,    
   senkrecht(M,d,ZAHL,FWD,Ds), Ds #< Fs,
   senkrecht(M,e,ZAHL,FWD,Es), square(Es),
   senkrecht(M,f,ZAHL,FWD,Fs), square(Fs), % s.o.
   senkrecht(M,m,LMs), palindrom(LMs),
   senkrecht(M,n,ZAHL,FWD,Ns), Ns #= Ew * Ew,
   senkrecht(M,o,ZAHL,FWD,Os), Os #> Hw, % s.o.
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl.
/*
1 8 9 1 3 1 
3 3 6 7 2 9 
2 2 4 2 4 6 
2 2 4 3 9 7 
6 1 8 6 6 6 
8 6 4 3 1 9 
CPU time 10.892 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0.
   
is_prime(X) =>
   X :: primes(1000).

square(X) => 	      % so klappt´s!
   SQ = [I*I : I in 1..31], X :: SQ.
   % element(_,SQ,X).

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).