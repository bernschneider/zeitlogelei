/* Zeit Logelei 2014/18 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main ?=>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,x,x,x,j},
	     {k,l,x,x,m,x},
	     {n,x,x,o,x,x},
	     {p,x,q,x,x,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,e,g,h,i,k,n,o,p,q], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,l,m,n], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,LAw), palindrom(LAw),
   waagrecht(M,e,ZAHL,FWD,Ew), is_prime(Ew),
   waagrecht(M,e,ZAHL,REV,REw), % s.u. 
   waagrecht(M,g,QSUM,FWD,QGw), QGw #= Pw,
   waagrecht(M,h,ZAHL,FWD,Hw), Hw #= Ms + Ew,
   waagrecht(M,i,ZAHL,FWD,Iw), Iw #= Bs * Bs,
   waagrecht(M,k,ZAHL,FWD,Kw), Kw #= Ls * Ls,
   waagrecht(M,n,QSUM,FWD,QNw), QNw #= Pw,
   waagrecht(M,o,ZAHL,FWD,Ow), multiple(REw,Ow),
   waagrecht(M,p,ZAHL,FWD,Pw), % s.o.
   waagrecht(M,p,ZAHL,REV,RPw), is_prime(RPw),
   waagrecht(M,q,QSUM,FWD,QQw), QQw #= 7,
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As #= Fs * Fs * Fs, 
   senkrecht(M,b,ZAHL,FWD,Bs), Bs mod 2 #= 1, % s.o.   
   senkrecht(M,c,ZAHL,FWD,Cs), Cs #= Es * Es, 
   senkrecht(M,d,ZAHL,FWD,Ds), Ds #= Ms * Ms,
   senkrecht(M,e,ZAHL,FWD,Es), % s.o.
   senkrecht(M,e,QSUM,FWD,QEs), QEs #= Fs, 
   senkrecht(M,f,ZAHL,FWD,Fs), multiple(Fs,Cs), % s.o. 
   senkrecht(M,j,ZAHL,FWD,Js), Js #= Ns * Ns,
   senkrecht(M,l,ZAHL,FWD,Ls), multiple(REw,Ls), % s.o.
   senkrecht(M,m,ZAHL,FWD,Ms), Ms mod 2 #= 1, % s.o.
   senkrecht(M,n,ZAHL,FWD,Ns), is_prime(Ns), % s.o. */
   Vars = MVars ++ [LAw,Ew,REw,QGw,Hw,Iw,Kw,QNw,Ow,Pw,RPw,QQw,As,Bs,Cs,Ds,Es,QEs,Fs,Js,Ls,Ms,Ns],
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl, fail.
/*
1 4 4 1 6 1 
7 6 1 4 4 2 
2 1 2 5 2 1 
8 9 1 1 3 6 
4 4 6 6 8 8 
1 4 4 1 1 1   CPU time 30.265 seconds, no more solutions, korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0, A #!= X.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..31], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).