/* Zeit Logelei 2014/06 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main =>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,h,x,x},
	     {i,x,x,j,k,x},
	     {l,m,n,x,o,x},
	     {p,x,q,r,x,s},
	     {t,x,x,x,u,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,c,g,h,i,k,l,n,p,r,t,u], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,i,j,m,o,q,s], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), Aw #= QCw,
   waagrecht(M,c,ZAHL,FWD,Cw), multiple(Kw,Cw),
   waagrecht(M,c,QSUM,FWD,QCw), % s.o.
   waagrecht(M,g,ZAHL,FWD,Gw), Gw #= Ss * Qs,
   waagrecht(M,h,ZAHL,FWD,Hw), Hw #= Uw * Uw,
   waagrecht(M,i,ZAHL,FWD,Iw), Iw #= Lw * Pw,
   waagrecht(M,k,ZAHL,FWD,Kw), is_prime(Kw), %. s.o.
   waagrecht(M,l,ZAHL,FWD,Lw), is_prime(Lw), % s.o.  
   waagrecht(M,n,ZAHL,FWD,Nw), Nw #= Hw * Kw,
   waagrecht(M,p,ZAHL,FWD,Pw), square(Pw),
   waagrecht(M,r,LRw),         palindrom(LRw),
   waagrecht(M,t,ZAHL,FWD,Tw), Tw #= Cw + Iw,
   waagrecht(M,u,ZAHL,FWD,Uw), is_prime(Uw), % s.o.
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), senkrecht(M,b,LBs), As #= prod(LBs),
   senkrecht(M,b,ZAHL,FWD,Bs), square(Bs),               % v- magic (Produkt von 4 Ziffern 3stellig)
   senkrecht(M,c,LCs), QpCs #= prod(LCs), LQpCs = new_list(3), LQpCs :: 1..9, to_num(LQpCs, 10, FWD, QpCs), palindrom(LQpCs), 
   senkrecht(M,c,ZAHL,FWD,Cs), % s.u.
   senkrecht(M,d,ZAHL,FWD,Ds), is_prime(Ds), 
   senkrecht(M,e,ZAHL,FWD,Es), square(Es),  
   senkrecht(M,f,ZAHL,FWD,Fs), Fs #= Is + Cs, 
   senkrecht(M,i,ZAHL,FWD,Is), % s.o.
   senkrecht(M,i,ZAHL,REV,RIs), square(RIs),
   senkrecht(M,j,QSUM,FWD,QJs), QJs #= Ss, 
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms),
   senkrecht(M,o,ZAHL,FWD,Os), square(Os),
   senkrecht(M,q,ZAHL,FWD,Qs), is_prime(Qs), % s.o. 
   senkrecht(M,s,ZAHL,FWD,Ss), % s.o.
   senkrecht(M,s,LSs), palindrom(LSs), 
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl. %, fail.
/*
1 4 2 3 1 8 
6 4 9 1 2 1 
5 1 7 5 1 9 
2 3 2 2 9 9 
2 2 5 1 6 1 
7 4 9 3 1 1
CPU time 6.113 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0, A #!= X.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..99], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).