/* Zeit Logelei 2020/01 Professor Knusi in Knuselien
https://www.zeit.de/2014/07/spiele-logelei-07 */
import cp.
main ?=>
  S=1, Z=2, R=3, B=4, V=5, D=6, K=7, N=8, L=9, F=10, W=11, Q=12, T=13, M=14,
  Name = ["Sum-Sin-Sar", "Zan-Zoe-Zil", "Rel-Rum-Rin", "Bum-Bum-Bah", "Von-Ver-Vix", "Din-Dol-Dum", "Ker-Ker-Kum", 
          "Nun-Nan-Nie", "Lin-Lan-Loh", "Fir-Fan-Faz", "Wau-Wau-Wau", "Quo-Qui-Qua", "Tik-Tak-Toe", "Moh-Mun-Mar"], 
  P = new_list(14), P :: 0..1, % 0 = lügt, 1 = sagt die Wahrheit
  foreach((P0,P1,G1,P2,G2) in 
      [(S, Z, 1, R, 0), % Sum-Sin-Sar: "Zan-Zoe-Zil sagt die Wahrheit, und Rel-Rum-Rin lügt." 
       (Z, W, 0, L, 1), % Zan-Zoe-Zil: "Wau-Wau-Wau lügt, und Lin-Lan-Loh sagt die Wahrheit." 
       (R, Q, 1, F, 0), % Rel-Rum-Rin: "Quo-Qui-Qua sagt die Wahrheit, und Fir-Fan-Faz lügt." 
       (B, K, 0, V, 1), % Bum-Bum-Bah: "Ker-Ker-Kum lügt, und Von-Ver-Vix sagt die Wahrheit." 
       (V, B, 0, F, 0), % Von-Ver-Vix: "Bum-Bum-Bah lügt, und Fir-Fan-Faz lügt." 
       (D, L, 1, W, 1), % Din-Dol-Dum: "Lin-Lan-Loh sagt die Wahrheit, und Wau-Wau-Wau sagt die Wahrheit." 
       (K, L, 1, T, 0), % Ker-Ker-Kum: "Lin-Lan-Loh sagt die Wahrheit, und Tik-Tak-Toe lügt." 
       (N, F, 0, Z, 1), % Nun-Nan-Nie: "Fir-Fan-Faz lügt, und Zan-Zoe-Zil sagt die Wahrheit." 
       (L, Q, 1, M, 0), % Lin-Lan-Loh: "Quo-Qui-Qua sagt die Wahrheit, und Moh-Mun-Mar lügt." 
       (F, V, 0, N, 1), % Fir-Fan-Faz: "Von-Ver-Vix lügt, und Nun-Nan-Nie sagt die Wahrheit." 
       (W, M, 0, S, 0)])% Wau-Wau-Wau: "Moh-Mun-Mar lügt, und Sum-Sin-Sar lügt."
    P[P0] #= 1 #<=> (P[P1] #= G1 #/\ P[P2] #= G2)
  end,  
  solve(P),
  Str = ["lügt", "sagt die Wahrheit"],
  foreach(I in 1..len(P))
    printf("%s %s%n", Name[I], Str[1+P[I]]) 
  end, nl, fail.
/* 
 */