/* Zeit Logelei 2014/26 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import sat, util.
main =>
   LM = {{a,b,c,d,e,f},
		 {g,x,h,x,i,x},
	     {j,k,l,x,x,m},
	     {n,x,o,p,q,x},
	     {r,x,x,x,x,x},
	     {s,x,x,x,x,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,d,g,h,i,j,n,o,q,r,s], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,k,l,m,p,r], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N), MVars = array_matrix_to_list(M), MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), % s.u.
   waagrecht(M,a,QSUM,FWD,QAw), square(QAw),
   waagrecht(M,d,ZAHL,FWD,Dw), square(Gw),
   waagrecht(M,g,ZAHL,FWD,Gw), square(Gw),
   waagrecht(M,h,ZAHL,REV,RHw), waagrecht(M,j,QSUM,FWD,QJw), RHw #= QJw,
   waagrecht(M,i,LIw), palindrom(LIw),
   waagrecht(M,j,ZAHL,FWD,Jw), Jw #= Ps * Ps, 
   waagrecht(M,n,ZAHL,FWD,Nw), is_prime(Nw),
   waagrecht(M,o,ZAHL,FWD,Ow), senkrecht(M,m,LMs),  Ow #= prod(LMs), % Ow = Querprodukt von M senkrecht
   % waagrecht(M,p,LPw), palindrom(LPw),
   waagrecht(M,q,ZAHL,FWD,Qw), no_prime(Qw),    
   waagrecht(M,r,ZAHL,FWD,Rw), Rw #= Aw * As,
   waagrecht(M,s,ZAHL,FWD,Sw), Sw #= Jw + Rw, 
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As mod 2 #= 1, % As ungerade; s.o.
   % senkrecht(M,a,ZAHL,REV,RAs), 
   senkrecht(M,b,ZAHL,FWD,Bs), is_prime(Bs),   
   senkrecht(M,c,ZAHL,FWD,Cs), is_prime(Cs),    
   senkrecht(M,d,ZAHL,FWD,Ds), % s.u.
   senkrecht(M,d,LDs), palindrom(LDs),
   senkrecht(M,e,ZAHL,FWD,Es), Es #= Dw * Ds,
   senkrecht(M,f,ZAHL,FWD,Fs), is_prime(Fs), 
   senkrecht(M,k,QSUM,FWD,QKs), square(QKs),
   senkrecht(M,l,QSUM,FWD,QLs), QLs #= RHw,
   senkrecht(M,m,LMs), increasing_strict(LMs),
   senkrecht(M,p,ZAHL,FWD,Ps), % s.o.
   senkrecht(M,p,LPs), prod(LPs) #= Ow,
   senkrecht(M,r,ZAHL,FWD,Rs), is_prime(Rs), 
   Vars = MVars, % ++ [LAw,LBs],
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl.
/*
2 7 7 5 2 9 
8 1 3 1 7 7 
2 1 2 5 2 1 
9 7 2 4 4 2 
7 8 3 6 3 3 
9 9 6 1 5 4 
CPU time 11.33 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0.
   
is_prime(X) =>
   X :: primes(1000).

no_prime(X) =>
   NP = [I : I in 1..99, not prime(I)],
   [X] :: NP.

square(X) =>
   SQ = [I*I : I in 1..31], X :: SQ.

is_cube(X) => 
   SC = [I*I*I : I in 1..9], X :: SC.

gleicheZiffern(L1, L2) =>
    foreach(Z in L1) element(_,L2,Z) end,
    foreach(Z in L2) element(_,L1,Z) end.

gleicheParitaet(L) =>
    Par :: 0..1,
    foreach(Z in L) Z mod 2 #= Par end.

nichtgleicheParitaet(L) =>
    sum([(Z mod 2 #= 0) : Z in L]) #> 0,
    sum([(Z mod 2 #= 1) : Z in L]) #> 0.

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).