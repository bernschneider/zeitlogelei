/* Zeit Logelei 2014/38 Kreuzzahl-Rätsel 
   Hinweis: Andere Kreuzzahlrätsel werden gelöst, indem man die Zeilen 11-18 und 24-53 anpasst 
   Die Startkoordinaten und Längen der Zahlen müssen nicht angegeben werden. Sie werden in waagrecht(..) bzw. senkrecht(...) aus den Buchstaben berechnet 
   Für die Längenberechnung wird mit membchk nach dem nächsten Buchstaben in der Zeile bzw. Spalte gesucht, für den eine Cons-Regel existiert */
import cp, util.
main =>
   LM = {{a,b,c,d,e,f},
		 {g,x,h,x,i,x},
	     {j,k,x,x,l,x},
	     {m,x,x,x,x,n},
	     {o,p,q,x,r,x},
	     {s,x,x,t,x,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,d,g,h,i,j,m,o,q,r,s,t], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,k,l,m,n,p,r], get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = 6, ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N),
   MVars = array_matrix_to_list(M),
   MVars :: 0..9,
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,Aw), 
   waagrecht(M,a,ZAHL,REV,RAw), square(RAw),
   waagrecht(M,d,ZAHL,FWD,Dw), Dw #= Ms + Iw,
   waagrecht(M,g,ZAHL,FWD,Gw), is_prime(Gw),
   waagrecht(M,h,ZAHL,FWD,Hw), is_prime(Hw),
   waagrecht(M,h,ZAHL,REV,RHw),
   waagrecht(M,i,ZAHL,FWD,Iw), multiple(Iw,Ls), % Ls ist Vielfaches von Iw
   waagrecht(M,j,ZAHL,FWD,Jw), Jw #= Ds + Mw,
   waagrecht(M,m,QSUM,FWD,Iw), % Die Quersumme von Mw ist Iw
   waagrecht(M,m,ZAHL,FWD,Mw),
   waagrecht(M,o,Ow), palindrom(Ow),    
   waagrecht(M,q,ZAHL,FWD,Qw), is_prime(Qw),
   waagrecht(M,r,ZAHL,FWD,Rw), is_prime(Rw),
   waagrecht(M,s,ZAHL,FWD,Sw), multiple(Gw,Sw),
   waagrecht(M,s,ZAHL,REV,RSw),
   waagrecht(M,t,ZAHL,FWD,Tw),
   waagrecht(M,t,ZAHL,REV,RTw), multiple(RPs,RTw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As #= Bs * Gw,
   senkrecht(M,b,ZAHL,FWD,Bs), is_prime(Bs),
   senkrecht(M,c,ZAHL,FWD,Cs), Cs #= Aw * As,    
   senkrecht(M,d,ZAHL,FWD,Ds), Ds #= Ns * Tw,
   senkrecht(M,e,ZAHL,FWD,Es), is_prime(Es),
   senkrecht(M,f,Fs), palindrom(Fs),
   senkrecht(M,k,ZAHL,FWD,Ks), is_prime(Ks),
   senkrecht(M,l,ZAHL,FWD,Ls), square(Ls),
   senkrecht(M,m,ZAHL,FWD,Ms), square(Ms),
   senkrecht(M,n,ZAHL,FWD,Ns), Ns #= RSw - RHw,
   senkrecht(M,p,ZAHL,FWD,Ps), is_prime(Ps),
   senkrecht(M,p,ZAHL,REV,RPs),
   senkrecht(M,r,Rs), palindrom(Rs),
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl.
   /*
5 2 2 4 7 3 
2 3 7 9 3 2 
9 8 6 4 6 3 
4 9 1 7 4 7 
4 4 3 1 7 3 
1 3 8 6 7 4 
CPU time 0.029 seconds korrekt */
multiple(A,X) => % X ist Vielfaches von A
   X mod A #= 0.
   
is_prime(X) =>
   Max = fd_max(X),
   foreach(I in 2..Max // 2)
      I #!= X #=> X mod I #> 0
   end.

square(X) => 	      % so klappt´s!
   SQ = [I*I : I in 1..1000],
   element(_,SQ,X).
   /*Max = fd_max(X), % so funktioniert es leider nicht immer!
   Tmp :: 0..Max,
   X #= Tmp*Tmp.*/

palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2) L[I] #= L[N + 1 - I] end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        waagrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

waagrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N, Len == 0, membchk(LM[R,Cn],WLetter)) Len := Cn - Col end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row,Col+I] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
        senkrecht(M,Letter,Tmp), to_num(Tmp, Base, Fwd, X).

senkrecht(M,Letter,Tmp) =>
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N, LM[R,C] = Letter) Row := R, Col := C end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N, Len == 0, membchk(LM[Rn,C],SLetter)) Len := Rn - Row end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		foreach(I in 0..Len-1) M[Row+I,Col] #= Tmp[I+1] end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).