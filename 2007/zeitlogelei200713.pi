/* Zeit Logelei Nr. 2007/13 BILD fehlt! (2007/12 ist eine Kreuzzahlrätsel)
https://www.zeit.de/2007/13/Spielen-Logelei-13
Kürzlich wurde eine Raumsonde aufgegabelt, die von einer außerirdischen Zivilisation auf Theta Zentauri kommt.
tbc */
import cp, util.

main => % dummy
    add(["DCFFEFD", "FCHEHAF", "DEAGHEDH"]). /*
Auflösung aus Nr. 14: 1092736842+1092736842=2185473684 */
add(L) => % das letzte Element von L ist die Summe der vorherigen Elemente
   Letter = keys(new_set(flatten(L))), % verwendete Buchstaben
   Digit = new_list(len(Letter)), Digit :: 0..9, % eine Ziffer je Buchstabe
   all_different(Digit),
   Zeilen = len(L) - 1, % Anzahl der Summanden, im Beispiel = 2
   Num = new_array(len(L)), % numerische Repräsentation der Elemente von L
   foreach(I in 1..len(L))
       S = [Digit[find_first_of(Letter, C)] : C in L[I]], % Convert Letter C -> Digit
       to_num(S, Num[I]), S[1] #!= 0
   end,
   sum([Num[I] : I in 1.. Zeilen]) #= Num[len(L)],
   solve(Digit ++ Num),
   foreach(I in 1..Zeilen) printf("%6d%n", Num[I]) end,
   printf("------%n"),
   printf("%6d%n%n", Num[len(L)]).

to_num(List, Num) => 
        Len = length(List),
        Num #= sum([List[I]*10**(Len-I) : I in 1..Len]).