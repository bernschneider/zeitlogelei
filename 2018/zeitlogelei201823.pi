import cp.
main =>
    R=1, T=2, Gr=3, L=4, B=5, Ge=6,
    Sechseck=1, Quadrat=2, Kreis=3, Dreieck=4, Fuenfeck=5, Stern=6,
    F = new_list(6), F :: 1..6, % F[C] = Form[Color]
    all_different(F),
    oegt(F,R,Sechseck,T,Fuenfeck),
    oegt(F,Gr,Quadrat,R,Sechseck),
    oegt(F,L,Dreieck,B,Kreis),
    oegt(F,Gr,Dreieck,T,Kreis),
    oegt(F,T,Fuenfeck,L,Kreis),
    oegt(F,B,Dreieck,Ge,Kreis),
    oegt(F,T,Fuenfeck,R,Kreis),
    oegt(F,Gr,Stern,L,Fuenfeck),
    oegt(F,R,Sechseck,Gr,Dreieck),
    oegt(F,Ge,Sechseck,B,Dreieck),
    oegt(F,L,Stern,B,Kreis),
    oegt(F,R,Dreieck,T,Quadrat),
    oegt(F,L,Fuenfeck,Gr,Kreis),
    solve(F),
    Color = ["rotes","türkises","grünes","lila","blauer","gelbes"],
    Form  = ["Sechseck", "Quadrat", "Kreis", "Dreieck", "Fuenfeck", "Stern"],
    foreach(C in 1..6)
        printf("%s %s%n", Color[C], Form[F[C]])
    end. 
/*
rotes Sechseck
türkises Fuenfeck
grünes Quadrat
lila Kreis
blauer Stern
gelbes Dreieck
Korrekt: "Elin hat ein gelbes Dreieck, ein grünes Quadrat, ein türkises Fünfeck, ein rotes Sechseck, einen lila Kreis sowie einen blauen Stern gemalt" */

/*  wenn ich 'Rotes Sechseck ögt türkises Fünfeck' sage, dann meine ich damit 'Wenn das Sechseck nicht rot ist, dann ist das Fünfeck nicht türkis'*/
oegt(F,C1,F1,C2,F2) =>
   F[C1] #!= F1 #=> F[C2] #!= F2.