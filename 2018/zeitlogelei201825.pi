/* Zeit Logelei 2018/ 25 
   Kreuzzahlen */
import cp.
import util.
main =>
   LM = {{a,b,c,d,e,f},
		 {g,x,x,x,h,x},
	     {i,x,x,j,x,x},
	     {k,x,l,x,x,m},
	     {n,x,x,o,x,x},
	     {p,x,x,x,q,x}}, get_global_map().put(lm,LM),       
   WLetter := [a,c,g,h,i,k,l,n,o,p,q], get_global_map().put(wl,WLetter), % alle Buchstaben mit waagrechter Bedingung
   SLetter := [a,b,c,d,e,f,j,k,l,m],   get_global_map().put(sl,SLetter), % alle Buchstaben mit senkrechter Bedingung
   N = len(LM), ZAHL = 10, QSUM = 1, FWD = 1, REV = 0, % cond arbeitet nicht mit Bool!
   M = new_array(N,N),
   MVars = array_matrix_to_list(M),
   MVars :: 0..9,
   Primes = primes(99), Squares = [I*I: I in 1..31],
   % Waagrecht:
   waagrecht(M,a,ZAHL,FWD,QPw), % s.u.
   palindrom([M[1,J] : J in 3..6]), % C Palindrom
   waagrecht(M,g,ZAHL,FWD,Gw), multiple(Gw, Ds), 
   waagrecht(M,h,ZAHL,FWD,Hw), Hw + Kw #= RKw,
   waagrecht(M,i,ZAHL,FWD,Iw), Iw #= Bs + Es,
   waagrecht(M,k,ZAHL,FWD,Kw), Kw #= QLw,
   waagrecht(M,k,ZAHL,REV,RKw), % s.o.
   waagrecht(M,l,QSUM,FWD,QLw), QLw :: Primes, %is_prime(QLw),
   waagrecht(M,n,ZAHL,FWD,Nw), Nw :: Squares,
   waagrecht(M,o,ZAHL,FWD,Ow), Ow :: Squares,
   waagrecht(M,p,ZAHL,REV,RPw), multiple(RPw,Ds),
   waagrecht(M,p,QSUM,FWD,QPw), % s.o.
   waagrecht(M,q,ZAHL,FWD,Qw), Qw  :: Primes, %is_prime(Qw),
   % Senkrecht:
   senkrecht(M,a,ZAHL,FWD,As), As :: Squares,
   senkrecht(M,b,ZAHL,FWD,Bs), multiple(Bs,Nw),
   senkrecht(M,c,ZAHL,FWD,Cs), Cs :: Squares,
   senkrecht(M,d,ZAHL,FWD,Ds), multiple(Nw,Ds),
   senkrecht(M,e,ZAHL,FWD,Es), Es #= Ow * Ow,
   senkrecht(M,f,ZAHL,FWD,Fs), Fs :: Squares,
   senkrecht(M,j,QSUM,FWD,QJs), multiple(Ds,QJs),
   senkrecht(M,k,ZAHL,FWD,Ks), Ks :: Squares,
   senkrecht(M,l,ZAHL,FWD,Ls), Ls :: Squares,
   senkrecht(M,m,ZAHL,FWD,Ms), Ms :: Squares,
   Vars = MVars,
   solve(Vars),
   foreach(I in 1..N) 
      foreach(J in 1..N) 
           printf("%d ", M[I,J])
      end,
      nl
   end,
   nl.
/*
2 1 1 4 4 1 
5 7 6 4 5 4 
6 2 9 7 6 4 
1 7 4 1 9 3 
4 8 4 6 7 6 
4 8 1 8 6 1 
CPU time 467.042 seconds. */

multiple(X,A) => % X ist Vielfaches von A
   X mod A #= 0.
   
palindrom(L) =>
    N = len(L),
    foreach(I in 1..N//2)
        L[I] #= L[N + 1 - I]
    end.

waagrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), WLetter = get_global_map().get(wl),
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(R in 1..N)
			foreach(C in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Row  > 0 then 
				foreach(Cn in Col+1..N)
					if Len == 0, membchk(LM[R,Cn],WLetter) then Len := Cn - Col end
				end,
				if Len = 0 then Len := N+1-Col end 
		    end 
		end,
	    Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
     		 M[Row,Col+I] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	

senkrecht(M,Letter,Base,Fwd,X) => % Base = 10/1 (1 = Quersumme), Fwd = FWD/REV (REV = Rückwert)
		LM = get_global_map().get(lm), SLetter = get_global_map().get(sl),        
		N = len(LM), Row := 0, Col := 0, Len := 0,
		foreach(C in 1..N)
			foreach(R in 1..N)
				if LM[R,C] = Letter then Row := R, Col := C end
			end,
			if Len == 0, Col > 0 then 
				foreach(Rn in Row+1..N)
					if Len == 0, membchk(LM[Rn,C],SLetter) then Len := Rn - Row end
				end,
				if Len = 0 then Len := N+1-Row end
			end 
		end,
   		Tmp = new_list(Len), Tmp :: 0..9,
   		to_num(Tmp, Base, Fwd, X),
   		foreach(I in 0..Len-1)
      		M[Row+I,Col] #= Tmp[I+1]
   		end,
		M[Row,Col] #!= 0.	
   
to_num(List, Base, Fwd, Num) => % Num = Wert der Liste
        Len = length(List), 
        Num #= cond(Fwd, sum([List[I]*Base**(Len-I) : I in 1..Len]), sum([List[I]*Base**(I-1) : I in 1..Len])).